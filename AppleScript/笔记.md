```applescript
display dialog "Hello, world!"
```

这段代码可以显示一个"hello, world!"弹窗



[github](https://github.com/WangGuibin/WGBToolsConfigRepository)

# 关于AppleScript脚本的一些使用总结

[CoderWGB](https://juejin.cn/user/3333374985383165/posts)

2021-01-24 10:232925

## 1. 暗黑模式切换

```apl
tell application "System Events"
	tell appearance preferences
		set dark mode to not dark mode
	end tell
end tell
```

## 2. 发送邮件

```apl
applescript
复制代码--设置参数
set recipientName to "xxx" --收件人
set recipientAddress to "xxx@gmail.com" --收件地址
set mailSubject to "使用AppleScript脚本自动化发邮件" --邮件主题
set mailContent to "这是一封来自AppleScript发出的测试邮件,请勿回复!!!" --邮件内容

--执行发送邮件操作
tell application "Mail"
	
	--创建信息
	set theMessage to make new outgoing message with properties {subject:mailSubject, content:mailContent, visible:true}
	
	--发送信息
	tell theMessage
		make new to recipient with properties {name:recipientName, address:recipientAddress}

		send
		
	end tell
end tell
```





## 3. 弹窗相关

一个按钮默认样式 ![一个按钮](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/a92821ee29424346a1e1fcd0da460595~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp) 一个按钮加空格格式化样式 ![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/24752c2e093b486ca424d87989fff83b~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp) 两个按钮样式 ![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/8c20264e6ec449e9ab1827ad1820190d~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp) 三个按钮样式 ![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/f8a4c14c674541aab22e24e9734746e6~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp) 没有`icon`样式 ![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/c0fda8a045b14743b3c4bee1bc9bdb0e~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp)

反正这个`dialog`感觉就很安卓

#### a. 普通弹窗

```apl
复制代码display dialog "这是内容" with title "这是标题" --默认带上取消和确认按钮
--设置一个OK按钮以及默认选中
display dialog "这是内容" with title "这是标题" buttons "OK" default button "OK"
--效果同上
display dialog "这是内容" with title "这是标题" buttons "OK" default button 1
-- 自定义多个按钮 (最多允许使用三个按钮。)
display dialog "这是内容" with title "这是标题" buttons {"OK","Cancel","HAHA"} default button "OK"
```

或者`alert` 这个就比较`iOS` ![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/d793bc0f784e4c588e747647f900714c~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp)

```apl
mipsasm
复制代码-- 与 dialog 类似布局上有所不同,按钮是居中纵向排列
display alert "hahhaha" buttons {"OK", "NO", "YES"} default button 2
```

#### b. 带图标的弹窗

```apl
applescript
复制代码--可以指定对话框的图标，icon 图标可以指定 note (普通) /stop (危险) /caution (警告) 三种类型 或者指向文件路径
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon caution

-- 自定义图标 注意图片格式应该为.icns格式的 可以去应用xx.app/contens/resources下面去找
set fileName to choose file "Select a Folder"
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon file fileName

-- 指定路径 桌面路径 + 文件名
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon file ((path to desktop as text) & "AppIcon.icns")

-- 或者这样
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon alias ((path to desktop as text) & "AppIcon.icns")
-- 转化一下
-- set fileName to ((path to desktop as text) & "AppIcon.icns")
set fileName to "Macintosh HD:Users:wangguibin:Desktop:AppIcon.icns"
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon file fileName

-- 直接使用App Store的图标
set fileName to "Macintosh HD:System:Applications:App Store.app:Contents:Resources:AppIcon.icns"
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon file fileName
```

#### c. 弹窗输入框表单

```apl
applescript
复制代码display dialog "表单" default answer "输入框内容" buttons {"按钮1", "按钮2", "按钮3"} default button 1 with icon caution
copy the result as list to {text_returned, button_pressed} --返回一个列表{文本,按钮}
```

#### d. 选择列表弹窗

![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/ea684c2cb86d45f68de08ef080938bbd~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp)

```apl
sqf
复制代码-- 默认单选 默认不选中的话直接设置 `default items {}` 即可
choose from list {"Shell", "Ruby", "Python", "Applescript", "Javascript", "Perl", "Dart"} with title "日期选择" with prompt "选择一门脚本语言" OK button name "学习" cancel button name "放弃" default items {"Python"}

-- 多选
choose from list {"Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday"} with title "日期选择" with prompt "选择一天或者多天" OK button name "确认选择" cancel button name "不选" default items {"Monday"} with multiple selections allowed
```

#### e. 选择文件和目录

选择文件

```apl
applescript
复制代码-- 选择文件 获取文件名 没有的话不会创建 只是返回一个路径 
choose file name with prompt "获取文件名"
```

选择目录

```apl
gradle
复制代码-- 注：其中prompt和default location参数同Choose File Name;另外invisibles指定显示隐藏 文件,multiple selections allowed可以多选,showing package contents显示包内容,省略时 则不显示隐藏文件/不可多选/不显示包内容
choose folder with prompt "选择目录" default location file "Macintosh HD:Users:mac:Desktop" with invisibles, multiple selections allowed and showing package contents
```

## 4. 通知

![img](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/563545fb86f0480883e541e1b7725c79~tplv-k3u1fbpfcp-jj-mark:3024:0:0:0:q75.awebp)

```apl
pgsql
复制代码-- 声音文件都在/System/Library/Sounds 
-- 其中Funk , Glass, Ping 这几种好听一些 
display notification "通知内容通知内容通知内容通知内容" with title "通知主标题" subtitle "副标题" sound name "Funk"
```

## 5. Shell 调用 AppleScript

适合简短的脚本语句

```bash
bash
复制代码#注意单引号shell无法传参 如需传参则需要使用双引号\转义
osascript -e 'display notification "通知内容通知内容通知内容通知内容" with title "通知主标题" subtitle "副标题" sound name "Funk"'
```

适合多行脚本,增加可读性

```bash
bash
复制代码#简单粗暴 直接使用重定向包含applescript语句即可
osascript <<EOF
set fileName to "Macintosh HD:System:Applications:App Store.app:Contents:Resources:AppIcon.icns"
display dialog "这是内容" with title "这是标题" buttons {"No", "Yes"} default button "Yes" with icon file fileName
set btn to (button returned of result)
get btn
EOF
# 返回值 "NO"或者"OK"
```

**更新于2021年1月26日 20:55:30** 这是自己封装的一个shell弹窗方法,如此也就可以一劳永逸了

```bash
bash
复制代码#!/bin/bash

# 1. 自定义通知消息 
# 传参 $1:消息描述 $2:通知标题 $3:通知副标题
function showNoteMessage(){
  osascript -e "display notification \"${1}\" with title \"${2}\" subtitle \"${3}\" sound name \"Funk\""
}
#test case:
#showNoteMessage "消息内容" "通知" "通知副标题"

# 2. 自定义弹窗(统一通用方法)
# 传参$1 弹窗消息内容
# 传参$2 弹窗标题
# 传参$3 按钮标题,多个用英文逗号隔开 
# 传参$4 默认选中的按钮标题或者按钮数字下标
# 传参$5 是否为输入框类型 传"0"或者"1" 默认"0"
# 传参$6 输入框默认占位内容,传不传无所谓,默认为空字符串 ""
# 传参$7 默认图标note/stop/caution 或者自定义文件路径(:格式路径可以通过choose file获取)  
# tips: 重要! 如果为输入框模式,那么输出结果就是{button returned:button,text returned:text} 否则只有按钮或者false 
function showCustomAlertMessage(){

if [[ -n "$4" ]]; then

	if [[ ${4} == *[!0-9]* ]]; then
     	defaultButton="default button \"${4}\""
     	else
     	defaultButton="default button ${4}"
	fi
else
	defaultButton=""
fi

#是否为输入框模式
if [[ "$5" = "1" ]]; then
	IS_InputMode="default answer \"${6}\""
	ReturnValue="get result"
else
	IS_InputMode=""
	ReturnValue="get the button returned of the result"
fi

if [[ -n "$7" ]]; then
  case ${7} in
	note)
	ICON="with icon note"
	;;
	stop)
	ICON="with icon stop"
	;;
	caution)
	ICON="with icon caution"
	;;
	*)
	ICON="with icon file \"${7}\""
	;;
 esac
else
 ICON="with icon file \"Macintosh HD:Applications:Xcode.app:Contents:Resources:Xcode.icns\""
fi

osascript <<EOF
	set buttonStr to "${3}"
	set oldDelimiters to AppleScript's text item delimiters
	set AppleScript's text item delimiters to ","
	set buttonList to every text item of buttonStr
	set AppleScript's text item delimiters to oldDelimiters
	get buttonList
	set btns to buttonList
	display dialog "${1}" with title "${2}" buttons btns ${IS_InputMode} ${defaultButton} ${ICON}
	${ReturnValue}  
EOF
}

# showCustomAlertMessage "弹窗消息内容" "弹窗标题" "按钮1,按钮2,按钮3" "按钮2"  "1" "占位内容" "note"
# showCustomAlertMessage "弹窗消息内容" "弹窗标题" "按钮1,按钮2,按钮3" 2  "0" "占位内容" "note"
#showCustomAlertMessage "弹窗消息内容" "弹窗标题" "按钮1,按钮2,按钮3" "按钮2" "0" "占位内容" "stop" 
#showCustomAlertMessage "弹窗消息内容" "弹窗标题" "按钮1,按钮2,按钮3" "按钮2" "0" "占位内容" "Macintosh HD:Applications:Xcode.app:Contents:Resources:Xcode.icns"

# 普通弹窗 一个ok就够了 就是一个确认的那种
function onlyConfirmButton(){
	showCustomAlertMessage "$1" "温馨提示" "好的" 1
} 
#onlyConfirmButton "登录成功!\n弹窗成功!"

# 多按钮弹窗 最多三个按钮 多了会报错
function mostButtons(){
	showCustomAlertMessage "$1" "温馨提示" "按钮1,按钮2,按钮3" "按钮3"
}
#mostButtons "最多只能定义三个按钮哦"

# 输入框弹窗
function showInputAlertMessage(){
	showCustomAlertMessage "$1" "$2" "取消,确认" 2 "1" ""
}
showInputAlertMessage "请输入密码" "登录验证"
```

## 6. AppleScript 调用 Shell

do shell script + shell脚本语句即可

```apl
mipsasm
复制代码set shellStr to do shell script "cd ~/Desktop;cat shell_var.sh"
display alert shellStr buttons {"OK"}
```

## 总结

`AppleScript`配合`Shell` 以及`Alfred` 感觉能玩出很多花样来,一些工具确实能提升不少效率和体验。 我平时玩的一些工具存放在这 [github.com/WangGuibin/…](https://link.juejin.cn/?target=https%3A%2F%2Fgithub.com%2FWangGuibin%2FWGBToolsConfigRepository)

## 参考文章

[applescript-快速入门](https://link.juejin.cn/?target=https%3A%2F%2Fwww.exchen.net%2Fapplescript-%E5%BF%AB%E9%80%9F%E5%85%A5%E9%97%A8.html)

[我的新玩具-AppleScript(四)](https://link.juejin.cn/?target=https%3A%2F%2Fblog.csdn.net%2Fu011238639%2Farticle%2Fdetails%2F56506056)

[applescript快速入门教程](https://link.juejin.cn/?target=https%3A%2F%2Fwww.cnblogs.com%2Fitcomputer%2Fp%2F10162392.html)

[AppleScript 脚本让 Mac 唱生日快乐歌](https://link.juejin.cn/?target=https%3A%2F%2Flucifr.com%2Fmake-your-mac-sing-happy-birthday-with-applescript%2F)

标签：

[命令行]()

作者：CoderWGB
链接：https://juejin.cn/post/6921144388802641933
来源：稀土掘金
著作权归作者所有。商业转载请联系作者获得授权，非商业转载请注明出处。



基础语法

一、这部分介绍注释,发出声音,弹窗

(1)简单入门

```applescript
<1>多行注释
(*
this is multi
comment
*)
<2>发出响声
beep 3
```

(2)

```
#表示使用"Daniel"(英国发音)发出声音,人员选择如下图1所示
say "Hello,world" using "Daniel" --或用"--"也可以表示单行注释
```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181222193245935-875729967.png)

```
图1<br>(3)弹窗
display alert ``"This is an alert"` `#弹窗示例
```

 二、变量赋值,string和number类型,字符串连接

```
set varName3 to true                    　　#设置布尔值
set varName1 to "This is a string.And I love"   　　　　　　 #这是字符串值变量
set varName2 to "12"                    　　#设置数字变量把双引号去掉即可
 
set x to varName2 as number                 　　#转换整形字符串为整形
set y to 2
set z to varName2 * y                   　　--乘法运算
display dialog z                    　　--把乘积以弹窗的方式展示,结果为24
 
--字符串连接展示
set myCountry to " China."
say varName1 & myCountry                　　#把连接后的句子读出来
```

三、列表操作

(1)列表常用操作,获取列表长度,列表元素赋值

```
set varName to {"A1", "B2", "C3"}
set varName2 to {"D4", "F5", "G6"}
set item 1 of varName to "arun"             　　#赋值varName列表的"A1"(第1个)为"arun"
set item -3 of varName2 to "arunyang.com"       　　#赋值varName2列表的"D4"(倒数第3个)为"arunyang.com"
set anotherVarName to varName & varName2
#set anotherVarName to items 2 through 2 of varName2    　　#items 2 through 2 of 即取值范围(表示从2元素开始,到第2个元素结束),这里为"B2"
set randomValue to some item of anotherVarName      　　#获取anotherVarName列表里的随机值
set lengthOfList to length of varName           　　#表示varName列表的长度
say randomValue                     　　#说出anotherVarName列表里的随机值
say lengthOfList                    　　#说出varName列表的长度
return anotherVarName
#返回=> {"arun", "B2", "C3", "arunyang.com", "F5", "G6"}

```

补充:

**set** myList **to** {"a", "b", "c", "d", "e", "f"}

**set** shortList **to** *items* 2 **through** 5 **of** myList  #返回=>{"b", "c", "d", "e"}

(2)列表整形元素合并操作

```
set numberVar to 2 as list
set numberVar2 to 4 as list
set numberVar3 to 5 as list
return numberVar & numberVar2 & numberVar3  #合并列表
#返回==>  {2, 4, 5}
```

(3)列表字符串元素合并操作

```
set StringVar to "String" as list
set listVar to {"Tacos"}
set StringVar2 to "arun"
return listVar & StringVar & StringVar2   #合并字符串列表
#返回=> {"Tacos", "String", "arun"}

```

(4)列表之间的合并

```
set list1 to {1}
set list2 to {2}
set list3 to list1 & list2
 
set list4 to {"", ""}
set reandom1 to some item of list4
 
#return list3           　　　　#返回=>{1, 2}
return reandom1     　　　　　　　　  #返回=>""
```

四、获取用户输入　

(1)弹窗按钮

```
set varName to display dialog "Choose an option" buttons {"option1", "option2"}    #如下图1
set varName1 to display dialog "Choose an option" default button "OK" #设置"OK"为默认按钮并高亮,如下图2
set buttonReturned to button returned of varName #返回=>选择的按钮,这里我选"option1"

```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181223200439828-1415158263.png)

　　　　　　　　　　图1

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181223200501314-125416814.png)

　　　　　　　　　　图2

(2)输入框

```
set varName to display dialog "Enter some text" default answer "" buttons {"button1", "button2", "button3"} default button "button3" 　　#弹出输入框,如下图1所示
#set varName1 to display dialog "Enter some text" default answer "Some default Input text" 　　　　　　　　　　　　　　　　　　　　　　　　　　  #设置弹出输入框的默认输入内容
set stringReturned to text returned of varName
get stringReturned 　　
```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181223201709476-259271235.png)

　　　　　　　　图1

 五、if条件语句

/=等同于≠

```
set var1 to 1
set var2 to 2
#if var1 ≠ var2 then    #等于=,不等于/=,小于<,大于>,大于等于>=,小于等于<=
#if var1 is equal to var2 then   #等于=
#if var1 is not equal to var2 then  #不等于
#if var1 is not less than var2 then  #不小于,即大于等于>=
set var3 to 3
set var4 to 4
#if var1 = var2 then #也可以改成or,后面可以接多个and或or语句
if var1 = var2 then
    display alert "Var1 is equal to var2"
else if var3 = var4 then
    display alert "var3 is equal var4!"
else
    display alert "Nothing returned true"
end if
```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181223211038525-363514518.png)

六、for循环

(1)重复固定次数

```
repeat 3 times
    say "This is an action!"
end repeat
```

(2)

```
set condition to false
repeat until condition is true
    say "This is an action"       #触发了一次说的动作,下次condition为true了,所以不会执行了
    set condition to true         #设置condition为true,这个是结束repeat的条件
end repeat

```

(3)

```
set condition to 0
repeat until condition = 3    #condition = 3 是退出条件
    say "This is an action"    #会重复3次
    set condition to condition + 1
end repeat
#Result返回3
```

七、Try and catch

```
set condition to false
repeat until condition is true
    try
        set age to display dialog "Enter your age" default answer "Age here"
        set age to text returned of age as number
        set condition to true                       　　　　#只要输入的是number,这个代码块没有任何error,就会结束循环
    on error                                 　　  #假如输入的是非number,就会报错,这里捕获错误,
        beep
        display alert "You must enter a number"
        set condition to false                      　　　　#设置condition为false就会进入下一个循环,直到condition为true
    end try
end repeat
display alert "Everything worked!"
```

八、函数和变量范围

(1)函数示例

```
on functionName(param1, param2)
    set var to param2 + 10
    display dialog param1 & " " & var
end functionName
 
functionName("A different string", 43)   #调用函数,如下图1所示

```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181223235131763-861130418.png)

　　　　　　　　　　图1

(2)

<1>函数内的变量为本地变量,函数外的变量为外部变量,两个变量互相隔离,都不能互相引用

<2>要想互相引用需要变成全局变量,即变量前加上global关键字

```
set var1 to "This is a variable!" 　　　　　　　　#var为external variable即外部变量
on function()
    try
        set var to "Inner variable" 　　#var1为本地变量(local variable)
        display dialog var        　　　#函数内不能访问外部变量var1,否则会报错"变量没有定义".如图1所示
    on error
        beep
        global var1　　　　　　　　　　　　
    end try
end function
function()
set var to "Potato pie"
display dialog var　　　　　　　　　　　　　　　　　 #如图2所示
display dialog var1　　　　　　　　　　　　　　　　　#如图3所示

```

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181224001106295-2145962720.png)

　　　　　　　　　　图1

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181224001124744-1879892252.png)

　　　　　　　　　　图2

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181224001155821-139716641.png)

　　　　　　　　　　图3

九、

可以通过词典来找相应的方法名称，将应用直接拖到 Dock 上的脚本编辑器图标，然后就会显示扩展的词典(如下图1)，在这里可以查看该应用支持的相应方法名称说明，比如Iterm2的词典如下图2所示: 

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181224084643707-1490035476.png)

　　　　图1

![img](https://mielgo-markdown.oss-cn-chengdu.aliyuncs.com/685013-20181224084816369-1184149220.png)

　　　　　　　　　　　　　　图2

 

 

十、使用脚本示例

(1)清空mac回收站

```
tell application "Finder"         #调用Finder程序
    empty the trash           #去清空回收站里面的垃圾
end tell                          #结束调用程序
```

(2)列出所选文件夹中所有的**文件夹**名称

```
set folderSelected to choose folder "Select a folder"
tell application "Finder"
    set listOfFolders to every folder of folderSelected
end tell
 
set theList to {}
repeat with aFolder in listOfFolders
    set temp to the name of aFolder
    set theList to theList & temp
end repeat
```

(3)用chrome浏览器打开指定网址

```
set myBlog to "http://www.arunyang.com"
 
# 告诉 Chrmoe 浏览器打开 URL
tell application "Google Chrome"
    # 新建一个 chrome 窗口
    set window1 to make new window
    tell window1
        set currTab to active tab of window1
        set URL of currTab to myBlog
    end tell
end tell
```

(4)ssh快速登录

```
-- Launch iTerm and log into multiple servers using SSH
tell application "iTerm"
    activate
    create window with default profile
    -- Read serverlist from file path below
    set Servers to paragraphs of (do shell script "/bin/cat /opt/applescript/serverlist")
    repeat with nextLine in Servers
        -- If line in file is not empty (blank line) do the rest
        if length of nextLine is greater than 0 then
            -- set server to "nextLine"
            -- set term to (current terminal)
            -- set term to (make new terminal)
            -- Open a new tab
            -- tell term
            tell current window
                create tab with default profile
                tell current session
                    write text "ssh-custom " & nextLine
                    -- sleep to prevent errors if we spawn too fast
                    do shell script "/bin/sleep 0.01"
                end tell
            end tell
        end if
    end repeat
    -- Close the first tab since we do not need it
    -- terminate the first session of the current terminal
    tell first tab of current window
        close
    end tell
end tell
```

(5)多屏登录

```
#! /usr/bin/osascript
-- List actions to perform
set Servers to paragraphs of (do shell script "/bin/cat /opt/applescript/serverlist")
-- Count number of Servers
--set num_actions to count of actions
set num_actions to count of Servers
 
-- Set cols and lines
set num_cols to round (num_actions ^ 0.5)
set num_lines to round (num_actions / num_cols) rounding up
 
-- Start iTerm
tell application "iTerm"
    activate
 
    # Create new tab
    tell current window
        create tab with default profile
    end tell
 
    -- Prepare horizontal panes
    repeat with i from 1 to num_lines
        tell session 1 of current tab of current window
            if i < num_lines then
                split horizontally with default profile
            end if
        end tell
    end repeat
 
    -- Prepare vertical panes
    set sessid to 1
    repeat with i from 1 to num_lines
        if i is not 1 then set sessid to sessid + num_cols
        if i is not num_lines or num_actions is num_cols * num_lines then
            set cols to num_cols - 1
        else
            set cols to (num_actions - ((num_lines - 1) * num_cols)) - 1
        end if
        repeat with j from 1 to (cols)
            tell session sessid of current tab of current window
                split vertically with default profile
            end tell
        end repeat
    end repeat
 
    -- Execute actions
    repeat with i from 1 to num_actions
        tell session i of current tab of current window
    set Server to item i of Servers
    if length of Server is greater than 0 then
      write text "ssh-ele " & Server
      do shell script "/bin/sleep 0.01"
    end if
        end tell
    end repeat
end tell
```
